mapscripts FallarborTown_MapScripts{
	MAP_SCRIPT_ON_TRANSITION {
	setrespawn(HEAL_LOCATION_FALLARBOR_TOWN)
	}
}

script grotto_solidago{
	msgbox(format("{COLOR GREEN}{SHADOW LIGHT_GREEN}Il y a un petit passage entre les arbres, voulez-vous vous faufiler entre eux?"),MSGBOX_YESNO)
	if (var(VAR_RESULT)==1){
		warp(MAP_LAVARIDGE_TOWN,3)
	}
	releaseall
	end
}

script restes{
	msgbox(format("Ce sont des restes de Pokémon..."))
	releaseall
	end
}

script porte{
	random(3)
	switch(var(VAR_RESULT)){
	case 0:
		msgbox(format("C'est fermé de l'intérieur."))
	case 1:
		msgbox(format("(une voix venant de l'intérieur) : IL EST OÙ LE RESPECT DE LA PROPRIÉTÉ PRIVÉE"))
	case 2:
		msgbox(format("C'est fermé à clé."))
	}
	releaseall
	end
}

script aventurier_mirabilis{
	faceplayer
	msgbox(format("Ho ho ho! Le temple de la forêt! Je suis venu à pied jusqu'ici depuis un endroit très lointain! On dit qu'un trésor est enfoui dans ce temple depuis des millénaires. Je veux mettre la main dessus à tout prix!"))
	releaseall
	end
}

script mirabilis_scout{
	faceplayer
	msgbox(format("Regarde mon magnifique Scarhino. Je l'ai combattu au moins 10 fois avant qu'il n'accepte de se faire capturer."))
	releaseall
	end
}

script mirabilis_lady{
	faceplayer
	msgbox(format("Diantre ! J'ai encore égaré mes clés!"))
	releaseall
	end
}

script mirabilis_dresseur{
	faceplayer
	msgbox(format("T'es un dresseur aussi ? J'ai tenté de chopper le badge d'Achille mais il m'a bien remis à ma place. Il est invincible crois-moi."))
	releaseall
	end
}

script mirabilis_ecolier{
	faceplayer
	msgbox(format("Avant de me lancer dans le défi des arènes, j'essaye d'accumuler un maximum de connaissances."))
	releaseall
	end
}

script mirabilis_campalta{
	msgbox(format("Bougre de merdre. Même 'po de poiscaille."))
	msgbox(format("J'en puis plus de cette vie, j'en puis plus de cette ville."))
	faceplayer
	msgbox(format("Tu m'écoutes ? J'suis originaire de Campalta, une ville à l'est d'Olnyris, j'po t'dire que c'est bien loin d'ici, en plus faut traverser la mer."))
	msgbox(format("De base j'étais venu ici pour bosser à Cellapolis Centre, mais d'puis que ces maudites roches ont fait leur apparition, je ne bosse pas et j'ai pas pu trouver de taff ailleurs. À Starupolis, ils demandent que des diplômés bac + j'sais pas combien, à Cellapolis Sud, diplômé ou pas c'est mort ils prennent que les gens du CDR, du coup je teste les autres villes. Comme tu peux le voir ici y a pas grand chose, donc je chôme."))
	msgbox(format("J'essaye de pêcher des Magicarpes pour les revendre mais ils sont trop malins pour mordre. Tout ce que je veux là, c'est économiser 15000¥ pour prendre un bateau pour rentrer chez moi."))
	releaseall
	end
}

script mirabilis_bet{
	faceplayer
	showmoneybox(14,1)
	msgbox(format("Salut l'ami. Ça te dit un petit pari? Chacun mise 1000¥, celui qui gagne emporte le tout. Ça va jouer à pile ou face. Pile c'est moi, face c'est toi. T'es prêt?"),MSGBOX_YESNO)
	if (var(VAR_RESULT)==1){
		checkmoney(1000)
		if (var(VAR_RESULT)==1){
		hidemoneybox
		removemoney(1000)
		showmoneybox(14,1)
		random(2)
		switch (var(VAR_RESULT)){
			case 0:
			msgbox(format("HA HA! Pile! À moi l'argent!"))
			case 1:
			msgbox(format("Face... Pourquoi suis-je aussi malchanceux!"))
			hidemoneybox
			addmoney(2000)
			showmoneybox(14,1)
		} 
		} else {
			msgbox(format("T'as pas assez."))
		}
	}
	msgbox(format("Hésite pas à revenir me voir si tu veux rejouer."))
	hidemoneybox
	releaseall
	end
}

script mirabilis_npc{
	faceplayer
	msgbox(format("Aah, Mirabilis! Cette ville est comme une oasis en plein désert, c'est un endroit chaleureux où tout voyageur égaré peut se reposer. Elle est réputée pour ses Sequoias et son vieux temple dont on ne sait pas grand chose."))
	showmoneybox(14,0)
	hidemoneybox
	removemoney(500)
	showmoneybox(14,0)
	msgbox(format("Tu feras gaffe aux faux guides la prochaine fois, haha."))
	hidemoneybox
	releaseall
	end
}

script mirabilis_cyclist{
	faceplayer
	msgbox(format("Si je gagne le triathlon, je remporte un vélo incroyablement utile, un vélo qui peut rouler sur l'eau comme sur la terre ferme! Il est importé de Galar. Je m'entraîne dur pour l'avoir. Je n'aurais plus à payer la traversée Olnyris Ouest-Est."))
	releaseall
	end
}

movement mirabilis_up{
	walk_up
}

script mirabilis_midori{
	faceplayer
	msgbox_mugshot(format("Ah t'es là. Archie a emmené Lisa chez un Taxi-Volant, elle ira à Portokov rencontrer ce fameux Flo. La pauvre, sa carrière de dresseuse a déjà pris fin."),MSGBOX_SIGN,MUGSHOT_MIDORI)
	msgbox_mugshot(format("Il m'a expliqué 2-3 trucs concernant le défi des arènes, contrairement à d'autres régions, il n'y a pas de bâtiment pour le combat d'arène, seulement un terrain. Les champions sont très occupés donc ils doivent choisir leurs adversaires au compte-goutte, d'où l'intérêt du défi! Le défi de cette arène consiste à battre le dominant du Temple Forestier, ça ne risque pas d'être simple!"),MSGBOX_SIGN,MUGSHOT_MIDORI_HAPPY)
	msgbox_mugshot(format("Le dominant est terriblement fort, je te propose de faire équipe pour le combattre."),MSGBOX_SIGN,MUGSHOT_MIDORI)
	msgbox_mugshot(format("Mais avant ça, j'aimerais que l'on fasse notre premier combat de dresseurs!"),MSGBOX_SIGN,MUGSHOT_MIDORI_HAPPY)
	trainerbattle_no_intro(TRAINER_BRENDAN_ROUTE_103_MUDKIP, format("Tu es devenu si fort en si peu de temps! Le prochain combat je gagnerai, c'est une promesse."))
	msgbox_mugshot(format("Je ne pouvais pas m'attendre à plus, laisse-moi soigner tes Pokémon."),MSGBOX_SIGN,MUGSHOT_MIDORI)
	special(HealPlayerParty)
	playfanfare(MUS_HEAL)
	msgbox_mugshot(format("Je file, me fais pas trop attendre."),MSGBOX_SIGN,MUGSHOT_MIDORI_HAPPY)
	turnobject(35,DIR_NORTH)
	applymovement(35,mirabilis_up)
	fadescreen(1)
	removeobject(35)
	fadescreen(0)
	setflag(FLAG_MIDORI_MIRABILIS)
	releaseall
	end
}

script mirabilis_center_man{
	faceplayer
	msgbox(format("J'adore les Sequoias de Mirabilis. Ils donnent un certain charme à la ville."))
	releaseall
	end
}

script mirabilis_center_boy{
	faceplayer
	msgbox(format("Le défi de l'arène il est insurmontable, j'ai beau entraîner mes Pokémon je n'arrive pas à battre le dominant. Peut-être ne suis-je pas né pour devenir dresseur."))
	releaseall
	end
}

script mystery_gift{
	faceplayer
	playbgm(MUS_HG_MYSTERY_GIFT,TRUE)
	msgbox(format("Bonjour. J'ai un cadeau pour vous mais avant, je dois vérifier votre identité. Veuillez saisir le code de réception."),MSGBOX_YESNO)
	if (var(VAR_RESULT)==1){
		special(DoWaldaNamingScreen)
		if (var(VAR_0x8004)==0){
		msgbox(format("Vous avez reçu un pokémon"))
		givemon(SPECIES_VICTINI,5,ITEM_CHOICE_SPECS)
		} else {
			msgbox(format("Je regrette, votre code est invalide."))
		}
	}
	playbgm(MUS_DP_POKE_CENTER_NIGHT,TRUE)
	releaseall
	end
}

script pokemon_util{
	lock
	multichoice(0,0,MULTI_CENTER,TRUE)
	switch (var(VAR_RESULT)){
		case 0:
			special(ChoosePartyMon)
			waitstate
			special(IsSelectedMonEgg)
			if (var(VAR_RESULT)==TRUE){
				msgbox(format("Le Pokémon choisi est un œuf, opération impossible."))
				break
			}
			if (var(VAR_0x8004)!=PARTY_NOTHING_CHOSEN){
				call(Common_EventScript_NameReceivedPartyMon)
				specialvar(VAR_RESULT, TryPutNameRaterShowOnTheAir)
				special(BufferMonNickname)
			}
		case 1:
			special(ChooseMonForMoveRelearner)
			waitstate
			special(IsSelectedMonEgg)
			if (var(VAR_RESULT)==TRUE){
				msgbox(format("Le Pokémon choisi est un œuf, opération impossible."))
				break
			}
			if (var(VAR_0x8005)==0){
				msgbox(format("Aucune attaque à apprendre à ce Pokémon, opération terminée."))
				break
			}
			if (var(VAR_0x8004)!=PARTY_NOTHING_CHOSEN){
				special(TeachMoveRelearnerMove)
				waitstate
			}
		case 2:
			    bufferpartymonnick(0, 0)
				msgbox("Votre {STR_VAR_1}...", MSGBOX_DEFAULT)
				specialvar(VAR_RESULT, GetHpIV)
				buffernumberstring(0, VAR_RESULT)
				specialvar(VAR_RESULT, GetAtkIV)
				buffernumberstring(1, VAR_RESULT)
				specialvar(VAR_RESULT, GetDefIV)
				buffernumberstring(2, VAR_RESULT)
				msgbox("IV de PV : {STR_VAR_1}.\nIV d'Attaque :  {STR_VAR_2}.\pIV de Défense : {STR_VAR_3}.", MSGBOX_DEFAULT)
				specialvar(VAR_RESULT, GetSpAtkIV)
				buffernumberstring(0, VAR_RESULT)
				specialvar(VAR_RESULT, GetSpDefIV)
				buffernumberstring(1, VAR_RESULT)
				specialvar(VAR_RESULT, GetSpeedIV)
				buffernumberstring(2, VAR_RESULT)
				msgbox("IV d'Attaque Spéciale : {STR_VAR_1}.\nIV de Défense Spéciale :  {STR_VAR_2}.\pIV de Vitesse : {STR_VAR_3}.", MSGBOX_DEFAULT)
	}
	releaseall
	end
}

script echange_miracle{
	lock
	faceplayer
	setvar(VAR_0x8008, SPECIES_EEVEE)
	copyvar(VAR_0x8004, VAR_0x8008)
	specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
	copyvar(VAR_0x8009, VAR_RESULT)
	msgbox(format("Bonjour, voulez-vous procéder à un échange miracle?"), MSGBOX_YESNO)
	if (var(VAR_RESULT)==1){
		special(ChoosePartyMon)
		waitstate
		copyvar(VAR_0x800A, VAR_0x8004)
		if (var(VAR_0x8004)!=PARTY_NOTHING_CHOSEN){
			copyvar(VAR_0x8005, VAR_0x800A)
			specialvar(VAR_RESULT, GetTradeSpecies)
			copyvar(VAR_0x800B, VAR_RESULT)
			copyvar(VAR_0x8004, VAR_0x8008)
			copyvar(VAR_0x8005, VAR_0x800A)
			special(CreateInGameTradePokemon)
			special(DoInGameTradeScene)
			waitstate
			bufferspeciesname(STR_VAR_1, VAR_0x8009)
		}
	} else {
		msgbox(format("À bientôt!"))
	}
	release
	end
}

script Bulbasaur{
	setwildbattle(SPECIES_BULBASAUR, 13)
	dowildbattle
	end
}

script Chikorita{
	setwildbattle(SPECIES_CHIKORITA, 12)
	dowildbattle
	end
}

script Lanterne{
	faceplayer
	checkitem(ITEM_LANTERN)
	if (var(VAR_RESULT)==0){
		msgbox(format("Bonjour dresseur! Je suis un employé chez V.N, dit Vitalis Navitas, tu dois sans doute nous connaître on invente tellement de choses!"))
		msgbox(format("Tu t'appelles {PLAYER} n'est-il pas? À Vitalis Navitas on cherche à aider les jeunes dresseurs. J'ai un petit quelque chose pour toi, il t'aidera à visiter le temple de la forêt."))
		giveitem(ITEM_LANTERN,1)
		msgbox(format("C'est une lanterne très spéciale, tu n'auras jamais besoin d'y mettre de l'huile, elle reçoit l'énergie nécessaire à son fonctionnement par induction, cette énergie vient directement de nos laboratoires, elle est totalement décarbonisée et est produite euh... Avec nos moyens de production d'énergie."))
		msgbox(format("N'est-ce pas fascinant ? De l'électricté gratuite partout dans la région? Vitalis Navitas fait beaucoup de choses pour améliorer la vie des individus."))
		//msgbox(format("Oh, j'ai un dernier truc à t'offrir!"))
		//setflag(FLAG_ENABLE_RIVAL_MATCH_CALL)
		//setflag(FLAG_ENABLE_MOM_MATCH_CALL)
		//setflag(FLAG_ENABLE_WALLY_MATCH_CALL)
		//setflag(FLAG_ADDED_MATCH_CALL_TO_POKENAV)
		//msgbox(format("Maintenant tu peux appeler tes amis et ta famille, directement avec ton V.N Tech (qui est aussi un produit de Vitalis Navitas!), j'espère que tu te serviras de tous mes cadeaux! Je te souhaite bon courage pour la suite! J'ai ajouté le numéro d'assistance de V.N."))

	} else {
		msgbox(format("Bonjour! Comment ça va? Tu devrais visiter le Centre Commercial de Vitalis Navitas à Portokov ou à la Ville-Monde, tu y trouveras sans doute quelque chose d'intéressant!"))
	}
	releaseall
	end
}

script noturhouse{
	msgbox(format("En haut se trouvent les chambres à coucher... Il vaudrait mieux ne pas monter"))
	releaseall
	end
}